#version 450

#define EPSILON 0.000001
#define NEG_EPSILON -0.000001
#define EPSILON1 1.000001

#define PI_F 3.14159265359
#define TWO_PI 6.28318530718
#define TO_RAD_F (PI_F / 180.0)
#define MAX_FLOAT 3.402823466e+38

#define VEC3_UP vec3(0.0, 0.999999995, 0.0001)

#define MAX_UINT 0xFFFFFFFF
#define INVALID_UINT MAX_UINT

// #define DEBUG_BOX
// #define NB_BOUNCES 4
#define NB_BOUNCES 1

// TODO: move to intersection pass.
struct Ray {
  vec3 origin;
  vec3 dir;
};

struct Intersection {
  vec3 emissive;
  uint materialIndex;
  vec2 uv;
  uint index;
  uint instance;
  bool emitter;
};

struct BRDFSample
{
  float pdf;
  vec3 dir;
};

struct Instance
{
  mat4 worldToModel;
  uint materialIndex;
  uint bvhRootIndex;
  uint vertexRootIndex;
  uint indexRootIndex;
};

struct BVHNode
{
  vec3 min;
  uint nextNodeIndex;
  vec3 max;
  uint primitiveStartIndex;
};

// TODO: split into multiple arrays?
struct Vertex
{
  vec3 position;
  uint padding_0;
  vec3 normal;
  uint padding_1;
  vec2 uv;
};

struct Material
{
  vec4 color;
  vec4 emission;
  float metalnessFactor;
  float roughnessFactor;
  uint albedoIndex;
  uint normalIndex;
  uint emissionIndex;
  uint metalRoughnessIndex;
  uint pad_0;
  uint pad_1;
};

struct Light
{
  vec4 normal;
  vec4 tangent;
  vec4 bitangent;
  float intensity;
  float padding_0;
  float padding_1;
  float padding_2;
};

/**
 * Layout 0.
 * TODO: move to layout 2 or 3 for perf.
 */

// layout(set = 0, binding = 0, rgba32f) uniform readonly image2D uRenderTarget;
// layout(set = 0, binding = 1, rgba32f) uniform writeonly image2D uRenderTargetWrite;
layout(set = 0, binding = 0, rgba32f) uniform writeonly image2D uRenderTargetWrite;

/**
 * Layout 0
 */

layout (set = 1, binding = 0) uniform RenderSettingsBuffer {
  uint width;
  uint height;
  uint instanceCount; // TODO: move to a unique scene uniform
  uint lightCount; // TODO: move to a unique scene uniform
  uint frameCount;
} RenderSettings;

layout (set = 1, binding = 1, std430) readonly buffer InstanceBuffer {
  Instance instances[];
};

layout (set = 1, binding = 2, std430) readonly buffer BVHNodeBuffer {
  BVHNode nodes[];
};

layout (set = 1, binding = 3, std430) readonly buffer IndexBuffer {
  uint indices[];
};

layout (set = 1, binding = 4, std430) readonly buffer VertexBuffer {
  Vertex vertices[];
};

layout (set = 1, binding = 5, std430) readonly buffer MaterialBuffer {
  Material materials[];
};

// @todo: move to uniform?
layout (set = 1, binding = 6, std430) readonly buffer LightBuffer {
  Light lights[];
};

// @todo: move to uniform?
layout (set = 1, binding = 7) uniform UniformsBuffer {
  float iFrame;
} Uniforms;

// layout(set = 1, binding = 8) uniform texture2D uProbe; // 10
// layout(set = 1, binding = 9) uniform sampler uProbeSampler; // 11

// layout(set = 1, binding = 10) uniform texture2DArray uTextures;

/**
 * Layout 2
 */

layout (set = 2, binding = 0) readonly uniform CameraUniformsBuffer {
  vec3 origin;
  float vFOV;
  vec3 up;
  float padding_0;
  vec3 right;
  float padding_1;
} CameraUniforms;

/* Utils */

uint
WangHash(inout uint seed)
{
  seed = uint(seed ^ uint(61)) ^ uint(seed >> uint(16));
  seed *= uint(9);
  seed = seed ^ (seed >> 4);
  seed *= uint(0x27d4eb2d);
  seed = seed ^ (seed >> 15);
  return seed;
}

float rand(inout uint seed)
{
  return float(WangHash(seed)) / 4294967296.0;
}

vec2
cartesianToEqui(vec3 dir)
{
  dir = dir.xzy;
  vec2 longlat = vec2(atan(dir.y, dir.x), acos(dir.z));
  longlat.x += PI_F;
  // Normalize
  return longlat / vec2(2.0 * PI_F, PI_F);
}

vec3 interpolate(vec3 v0, vec3 v1, vec3 v2, vec3 barycentric)
{
  return (
    barycentric.x * v0 +
    barycentric.y * v1 +
    barycentric.z * v2
  );
}

vec2 interpolate(vec2 v0, vec2 v1, vec2 v2, vec3 barycentric)
{
  return (
    barycentric.x * v0 +
    barycentric.y * v1 +
    barycentric.z * v2
  );
}

vec3
transformPosition(vec3 position, mat4 transform)
{
  vec4 pos = transform * vec4(position, 1.0);
  return pos.xyz / pos.w;
}

vec3
transformDirection(vec3 direction, mat4 transform)
{
  return normalize((transform * vec4(direction, 0.0)).xyz);
}

Ray
transformRay(inout Ray ray, mat4 transform)
{
  Ray result;
  result.origin = transformPosition(ray.origin, transform);
  result.dir = transformDirection(ray.dir, transform);
  return result;
}

// Implementation of Hammersley Points on the Hemisphere
vec3
randomCosineWeightedVector(inout uint seed)
{
  // To avoid to use a second sine and a normalization, it's possible to
  // use directly the random number in [0.0; 1.0] and scale the generated
  // `x` and `z` coordinates by it to obtain a normalized vector.
  // The code below is equivalent to:
  //   x = cos(theta), y = sin(phi), z = sin(theta);
  //   normalize(x, y, z);

  float theta = rand(seed) * TWO_PI;
  float r = max(EPSILON, rand(seed));
  float rLen = sqrt(max(EPSILON, 1.0 - r));

  float z = sqrt(r); // weights the samples to tend the normal
  float x = cos(theta) * rLen; // weights to preserve normalization
  float y = sin(theta) * rLen; // weights to preserve normalization

  return vec3(x, y, z);
}

/* Intersection */

Vertex
getVertex(uint index)
{
  return vertices[indices[index]];
}

/* Shading */

BRDFSample
LambertBRDF(vec3 normal, inout uint randState)
{
  vec3 worldUp = abs(normal.z) < 0.9999 ? vec3(0, 0, 1) : vec3(1, 0, 0);
  vec3 tangent = normalize(cross(worldUp, normal));
  vec3 bitangent = cross(normal, tangent);
  vec3 localDir = randomCosineWeightedVector(randState);

  BRDFSample brdf;
  brdf.dir = normalize(tangent * localDir.x + bitangent * localDir.y + localDir.z * normal);
  brdf.pdf = 0.5; // Should be PI/2 but cancels out with cosine sampling.
  return brdf;
}

// TODO: move to intersection pass.
Ray
generateRay(ivec2 coords)
{
  Ray ray;

  float half_w = float(RenderSettings.width) * 0.5;
  float half_h = float(RenderSettings.height) * 0.5;

  vec3 clip = vec3(
    coords.x - half_w,
    coords.y - half_h,
    half_h / tan(CameraUniforms.vFOV * 0.5)
  );

  ray.origin = CameraUniforms.origin;

  // TODO: pack direction directly?
  vec3 dir = normalize(cross(CameraUniforms.up, CameraUniforms.right));

  ray.dir = clip.x * CameraUniforms.right + clip.y * CameraUniforms.up + clip.z * dir;
  ray.dir = normalize(ray.dir);

  return ray;
}

float
intersectPlane(Ray ray, vec3 normal, vec3 origin, vec3 edge01, vec3 edge02)
{
  float NdotD = dot(normal, ray.dir);
  if (NdotD < EPSILON) { return MAX_FLOAT; }

  float t = dot(normal, origin - ray.origin) / NdotD;
  if (t < EPSILON) { return MAX_FLOAT; }

  vec3 intersection = (ray.origin + ray.dir * t) - origin;

  // Check not before first edge.
  float interProj = dot(edge01, intersection);
  if (interProj < EPSILON || interProj > dot(edge01, edge01)) { return MAX_FLOAT; }

  interProj = dot(edge02, intersection);
  if (interProj < EPSILON || interProj > dot(edge02, edge02)) { return MAX_FLOAT; }

  return t;
}

// TODO: implement watertight version of Ray-Triangle intersection, available
// behind a flag

// Implementation of:
// MÃ¶ller, Tomas; Trumbore, Ben (1997). "Fast, Minimum Storage Ray-Triangle Intersection"
float
intersectTriangle(Ray ray, uint startIndex, inout vec2 uv)
{
  // TODO: pre-process edge?
  // Maybe not really useful if decide to add skinning in shader.
  vec3 v0 = getVertex(startIndex).position;
  vec3 v1 = getVertex(startIndex + 1).position;
  vec3 v2 = getVertex(startIndex + 2).position;

  vec3 e1 = v1 - v0;
  vec3 e2 = v2 - v0;

  vec3 p = cross(ray.dir, e2);
  float det = dot(e1, p);

  // Ray is parralel to edge.
  if (det <= NEG_EPSILON) { return MAX_FLOAT; }
  if (det > NEG_EPSILON && det < EPSILON) { return MAX_FLOAT; }

  float invDet = 1.0 / det;

  // Computes Barycentric coordinates.
  vec3 centered = ray.origin - v0;

  float u = dot(centered, p) * invDet;
  if (u < EPSILON || u > EPSILON1) { return MAX_FLOAT; }

  vec3 q = cross(centered, e1);
  float v = dot(ray.dir, q) * invDet;
  if (v < EPSILON || u + v > EPSILON1) { return MAX_FLOAT; }

  uv = vec2(u, v);
  return dot(e2, q) * invDet;
}

float
intersectAABB(vec3 origin, vec3 inverseDir, vec3 aabbMin, vec3 aabbMax)
{
  // Ray is assumed to be in local coordinates, ie:
  // ray = inverse(objectMatrix * invCameraMatrix) * ray

  // Equation of ray: O + D * t

  vec3 tbottom = inverseDir * (aabbMin - origin);
  vec3 ttop = inverseDir * (aabbMax - origin);

  vec3 tmin = min(ttop, tbottom);
  vec3 tmax = max(ttop, tbottom);

  float smallestMax = min(min(tmax.x, tmax.y), min(tmax.x, tmax.z));
  float largestMin = max(max(tmin.x, tmin.y), max(tmin.x, tmin.z));

  if (smallestMax < largestMin || smallestMax < 0.0) { return MAX_FLOAT; }
  return (largestMin > 0.0) ? largestMin : smallestMax;
}

float
intersectAABB(Ray ray, vec3 aabbMin, vec3 aabbMax)
{
  return intersectAABB(ray.origin, 1.0 / ray.dir, aabbMin, aabbMax);
}

float
sceneHit(Ray ray, inout Intersection intersection)
{
  float dist = MAX_FLOAT;
  vec2 uv = vec2(0.0);
  uint materialIndex = INVALID_UINT;

  for (uint i = 0; i < RenderSettings.lightCount; ++i)
  {
    Light light = lights[i];
    vec3 origin = vec3(light.normal.w, light.tangent.w, light.bitangent.w);
    float t = intersectPlane(
      ray, - light.normal.xyz, origin, light.tangent.xyz, light.bitangent.xyz
    );
    if (t > 0.0 && t < dist)
    {
      intersection.emissive = vec3(1.0, 0.9, 0.8) * light.intensity;
      intersection.emitter = true;
      dist = t;
    }
  }

  for (uint i = 0; i < RenderSettings.instanceCount; ++i)
  {
    Instance instance = instances[i];

    // Performs intersection in model space.
    Ray rayModel = transformRay(ray, instance.worldToModel);
    vec3 rayInverseDir = 1.0 / rayModel.dir;

    #ifdef DEBUG_BOX
    float depth = 1.0;
    #endif

    uint nextIndex = instance.bvhRootIndex;

    while (nextIndex != INVALID_UINT)
    {
      BVHNode node = nodes[nextIndex];

      float d = intersectAABB(rayModel.origin, rayInverseDir, node.min, node.max);
      if (d < MAX_FLOAT && d < dist)
      {
        if (node.primitiveStartIndex != INVALID_UINT)
        {
          float t = intersectTriangle(rayModel, node.primitiveStartIndex, uv);
          if (t > 0.0 && t < dist)
          {
            intersection.uv = uv;
            intersection.index = node.primitiveStartIndex;
            intersection.instance = i;
            // TODO: remove material as we now pass instance directly.
            intersection.materialIndex = instance.materialIndex;
            intersection.emitter = false;
            dist = t;
          }
          nextIndex = node.nextNodeIndex;
          continue;
        }
        nextIndex++;
      }
      else
      {
        nextIndex = node.nextNodeIndex;
      }

      // // Node is a leaf.
      // if (node.primitiveStartIndex != INVALID_UINT)
      // {
      //   #ifdef DEBUG_BOX
      //   intersection.emissive += vec3(0.0, 0.0, 0.001);
      //   #endif

      //   float t = intersectTriangle(rayModel, node.primitiveStartIndex, uv);
      //   if (t > 0.0 && t < dist)
      //   {
      //     intersection.uv = uv;
      //     intersection.index = node.primitiveStartIndex;
      //     intersection.instance = i;
      //     // TODO: remove material as we now pass instance directly.
      //     intersection.materialIndex = instance.materialIndex;
      //     intersection.emitter = false;
      //     dist = t;
      //   }
      //   nextIndex = node.nextNodeIndex;
      //   continue;
      // }

      // float d = intersectAABB(rayModel.origin, rayInverseDir, node.min, node.max);
      // if (d < MAX_FLOAT && d < dist)
      // {
      //   #ifdef DEBUG_BOX
      //   intersection.emissive += vec3(1.0 - (1.0 / depth), 0.0, 0.0);
      //   depth += 0.55;
      //   #endif
      //   nextIndex++;
      //   continue;
      // }
      // nextIndex = node.nextNodeIndex;
    }
  }

  // intersection.emissive.b = test / 8000.0;
  return dist;
}

layout(local_size_x = 8, local_size_y = 8) in;
void main()
{
  ivec2 idx = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);

  uint randState = uint(
    idx.x * uint(1973) + idx.y * uint(9277) + uint(Uniforms.iFrame) * uint(26699)
  ) | uint(1);

  Ray ray = generateRay(idx);

  vec3 throughput = vec3(1.0);
  vec4 radiance = vec4(vec3(0.0), 1.0);

  Intersection intersection;
  #ifdef DEBUG_BOX
  for (uint bounces = 0; bounces < 1; ++bounces)
  #else
  for (uint bounces = 0; bounces < NB_BOUNCES; ++bounces)
  #endif
  {
    intersection.emissive = vec3(0.0);
    intersection.emitter = false;

    float t = sceneHit(ray, intersection);

    #ifdef DEBUG_BOX
    radiance = vec4(intersection.emissive, 1.0);
    break;
    #endif

    if (t >= MAX_FLOAT)
    {
      vec2 uv = cartesianToEqui(ray.dir);
      // vec3 sky = texture(sampler2D(uProbe, uProbeSampler), uv).rgb;
      vec3 sky = vec3(0.05, 0.05, 0.2);
      radiance.rgb += throughput * sky;
      break;
    }

    radiance.rgb += throughput * intersection.emissive;

    if (intersection.emitter)
    {
      // Sample emitter here.
      break;
    }

    vec3 barycentric = vec3(1.0 - intersection.uv.x - intersection.uv.y, intersection.uv);
    // float barycentricW = 1.0 - uv.x - uv.y;

    Vertex vertex0 = getVertex(intersection.index);
    Vertex vertex1 = getVertex(intersection.index + 1);
    Vertex vertex2 = getVertex(intersection.index + 2);

    vec3 normal = interpolate(vertex0.normal, vertex1.normal, vertex2.normal, barycentric);
    vec2 uv = interpolate(vertex0.uv, vertex1.uv, vertex2.uv, barycentric);

    // ð¿ð‘Ÿ ð›šð‘Ÿ â‰ˆ ð‘ ð‘“ð‘Ÿ ð›šð‘–,ð›šð‘Ÿ ð¿ð‘– ð›šð‘– cosðœƒð‘–â¡

    // We sample in a cosine weighted hemisphere, so basially we remove
    // the 2PI term and the multiplication by cos(ðœƒ) as the samples are already
    // sampled in a cosine hemisphere.
    BRDFSample brdf = LambertBRDF(normal, randState);

    // Material mat = materials[intersection.materialIndex];
    Material mat;
    mat.color = vec4(1.0, 0.0, 0.0, 1.0);

    vec3 albedo = mat.color.rgb;
    if (mat.albedoIndex != INVALID_UINT)
    {
      // albedo *= texture(sampler2DArray(uTextures, uProbeSampler), vec3(uv, mat.albedoIndex)).rgb;
      // albedo *= ;
    }

    vec3 directRadiance = albedo * brdf.pdf;
    throughput *= directRadiance;

    // Diffuse bounce.
    ray.origin += t * ray.dir + normal * EPSILON;
    ray.dir = brdf.dir;

    // Russian Roulette.
    float randnb = bounces > 1 ? rand(randState) : 0.0;
    float probability = max(throughput.x, max(throughput.y, throughput.z)) + EPSILON;
    if (randnb > probability) { break; }

    throughput /= probability;
  }

  // vec4 c = imageLoad(uRenderTarget, idx);
  // if (RenderSettings.frameCount == 1) { c = vec4(0.0); }
  // imageStore(uRenderTargetWrite, idx, c + radiance);
  imageStore(uRenderTargetWrite, idx, radiance);
  // if (instances[0].worldToModel[0].x >= 0.99) {
  //   imageStore(uRenderTargetWrite, idx, vec4(0.5, 1.0, 1.0, 1.0));
  // }
  // imageStore(uRenderTargetWrite, idx, vec4(0.2, 1.0, 0.0, 1.0));
}
